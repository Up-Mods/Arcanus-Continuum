plugins {
	id 'maven-publish'
	alias libs.plugins.quilt.loom
}

def ENV = System.getenv()
def buildTime = ENV.BUILD_TIME ?: new Date().format('yyyyMMddHHmmss')

def javaVersion = 17

//TODO increment when updating the compendium
def book_version = 1

group = "dev.cammiescorner"
archivesBaseName = "arcanus-continuum"

boolean isPreviewBuild = !ENV.TAG || ENV.TAG.matches(".+-.+")
def buildNumber = !ENV.TAG ? ("${ENV.BUILD_NUMBER ? "build.${ENV.BUILD_NUMBER}" : buildTime}-${libs.versions.minecraft.get()}") : ""
version = (ENV.TAG ?: "development") + ((isPreviewBuild && !ENV.TAG) ? "+${buildNumber}" : "")

// messy workaround for bug in quilt loom 1.3.3
project.configurations.configureEach {
	exclude(group: "net.fabricmc", module: "fabric-loader")
	exclude(group: "net.fabricmc.fabric-api")
}

repositories {
	maven {
		name = "QuiltMC Snapshots"
		url = "https://maven.quiltmc.org/repository/snapshot"
	}
	maven {
		url = "https://maven.shedaniel.me"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com"
	}
	maven {
		name = "Ladysnake"
		url = "https://maven.ladysnake.org/releases"
	}
	maven {
		url = "https://maven.blamejared.com"
	}
	maven {
		url = "https://maven.kosmx.dev"
	}
	maven {
		url = "https://maven.jamieswhiteshirt.com/libs-release"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		name = "CurseMaven"
		url = "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	maven {
		name = "SonaType Snapshots"
		url = "https://oss.sonatype.org/content/repositories/snapshots"
	}
	maven {
		name = "JitPack"
		url = "https://jitpack.io"
	}
}

dependencies {
	minecraft libs.minecraft

	mappings variantOf(libs.quilt.mappings) { classifier "intermediary-v2" }

	modImplementation libs.quilt.loader
	modImplementation libs.quilted.fabric.api
	modRuntimeOnly libs.quilted.fabric.api.deprecated

	include libs.mixinextras
	implementation libs.mixinextras
	annotationProcessor libs.mixinextras

	modImplementation libs.bundles.cca

	modCompileOnly libs.patchouli
	modLocalRuntime libs.patchouli

	modImplementation libs.midnightlib
	modImplementation libs.smartbrainlib

	include libs.reach.attr
	modImplementation libs.reach.attr

	// first person model
	modImplementation libs.firstperson

	// pehkui
	modCompileOnly libs.pehkui
	modRuntimeOnly libs.pehkui
	modLocalRuntime libs.kanos

	// modmenu
	modLocalRuntime libs.modmenu

	// LazyDFU
	modLocalRuntime libs.lazydfu
}

loom {
	accessWidenerPath = file("src/main/resources/arcanuscontinuum.accesswidener")
}

processResources {
	inputs.property "version", version
	inputs.property "book_version", book_version

	filesMatching("quilt.mod.json") {
		expand "version": version
	}
	filesMatching("**/patchouli_books/compendium_arcanus/book.json") {
		expand "book_version": book_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release.set(javaVersion)
}

java {
	withSourcesJar()
	toolchain {
		languageVersion = JavaLanguageVersion.of(javaVersion)
	}
}

jar {
	from("LICENSE.md") {
		rename { "LICENSE_${archivesBaseName}.md" }
	}

	manifest.mainAttributes(
		"Implementation-Title": project.archivesBaseName,
		"Implementation-Version": project.version,
		"Maven-Artifact": "${project.group}:${project.archivesBaseName}:${project.version}".toLowerCase(Locale.ROOT),
		"Built-On-Minecraft": libs.versions.minecraft.get(),
		"Built-On-Java": "${System.getProperty("java.vm.version")} (${System.getProperty("java.vm.vendor")})"
	)
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_UPLOAD_URL) {
			maven {
				url = ENV.MAVEN_UPLOAD_URL
				credentials {
					username = ENV.MAVEN_UPLOAD_USERNAME
					password = ENV.MAVEN_UPLOAD_PASSWORD
				}
			}
		}
	}
}
